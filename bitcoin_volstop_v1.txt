//Author - Jack Gilcrest
//@version=2
strategy("BTC VolStop Signals", overlay=true, commission_value = 0.25, default_qty_type = strategy.percent_of_equity, default_qty_value = 100)

//initialize backtest range
FromMonth = input(defval = 1, title = "From Month", minval = 1)
FromDay   = input(defval = 1, title = "From Day", minval = 1)
FromYear  = input(defval = 2010, title = "From Year")
ToMonth   = input(defval = 12, title = "To Month", minval = 1)
ToDay     = input(defval = 31, title = "To Day", minval = 1)
ToYear    = input(defval = 2020, title = "To Year")
startDate = time > timestamp(FromYear, FromMonth, FromDay, 00, 00)
endDate = time < timestamp(ToYear, ToMonth, ToDay, 23, 59)
withinTimeRange = startDate and endDate

//define variables
Factor=input(3, minval=1,maxval = 100, title='ATR Multiplier')
Pd=input(7, minval=1,maxval = 100, title='ATR Lookback Period')
rrm = input(3, title='Risk to Reward Multiplier')
sd = input(1, title='Stop Deviations')
sp = input(20, title='Bars Before Trailing Stops ') 

//find up and down atr stops from price anchor
Up=hl2-(Factor*atr(Pd))
Dn=hl2+(Factor*atr(Pd))

//Determine actual up and down stops
TrendUp=close[1]>TrendUp[1]? max(Up,TrendUp[1]) : Up
TrendDown=close[1]<TrendDown[1]? min(Dn,TrendDown[1]) : Dn

//Determine trend direction and set trend value accordingly
Trend = close > TrendDown[1] ? 1: close< TrendUp[1]? -1: nz(Trend[1],1)
trendVal = Trend==1? TrendUp: TrendDown

//Trend Parameters
isUptrend = Trend == 1
isTrendChanged = Trend != Trend[1]

//Stop and Target Line Calculations
span = abs(trendVal - trendVal[1])
barsSinceOrder = isUptrend ? isTrendChanged ? 0 : barsSinceOrder[1] + 1 : -1
trailingStop = isUptrend ? barsSinceOrder > sp : false
stopVal = trailingStop ? TrendUp : stopline[1]
finalStopVal = isTrendChanged ? hl2 - (sd * span) : stopVal

//Parameterize Stop and Target Lines
stopline = isUptrend ? finalStopVal : -1
targetline = isUptrend ? isTrendChanged ? hl2 + (sd * rrm * span) : targetline[1] : -1

//Plot data on chart
linecolor = Trend == 1 ? green : red
plot(trendVal, color = linecolor , style = line , linewidth = 2,title = "Trend Line")
plot(targetline, color = blue, style = line, linewidth = 2, title = 'Target Line')
plot(stopline, color = purple, style = line, linewidth = 2, title = 'Stop Line') 

//Trading conditions
stopCondition = close < stopline and withinTimeRange
buyCondition = Trend == 1 and close > stopline and not stopCondition and close < targetline and withinTimeRange
sellCondition = close >= targetline and withinTimeRange

//Trade execution
strategy.entry("Buy", strategy.long, when = buyCondition)
strategy.close("Buy", when = stopCondition or sellCondition)
